import '/os'
import '/socket'
import '/logging'

import '/asyncio'
import '/asyncio/CancelledError'
import '/asyncio/IncompleteReadError'

import 'util'
import 'http'
import 'request'
import 'response'
import 'response/Abort'

SOCKET_KEEPALIVE = hasattr socket 'SO_KEEPALIVE'


Server = subclass object where
  __init__ = handler onerror: None timeout: 15 static_dir: None ~>
    @timeout = timeout
    @handler = handler
    @onerror = onerror or @error
    @log     = logging.getLogger 'dogeweb'
    @static  = static_dir
    None

  #: Synchronously start a server. See `asyncio.start_server` for arguments.
  #:
  #: run :: * ** -> ()
  #:
  run = *: a loop: None **: k ~>
    loop = loop or asyncio.get_event_loop!
    srv  = loop.run_until_complete $ util.start_server *: a **: k loop: loop $ self loop
    except _       => loop.run_forever!
           finally => srv.close!

  #: Create a connection handler. Use with `asyncio.start_server` or something similar.
  #:
  #: () :: EventLoop -> StreamReader StreamWriter -> coroutine a
  #:
  __call__ = loop ~> reader writer -> except
    err =>
      SOCKET_KEEPALIVE =>
        sock = writer.transport.get_extra_info 'socket'
        sock.setsockopt socket.SOL_SOCKET socket.SO_KEEPALIVE 1

      while True =>
        req = request.Request self @handler loop reader writer @log *:
          except
            e2 => yield from $ util.timelimit @timeout loop: loop $ http.io_request reader
            e2 :: ValueError          => @log.debug 'bad request' exc_info: 1, raise CancelledError
            e2 :: IncompleteReadError => raise CancelledError

        resp = except
          outer => except
            inner => yield from $ response.responsify $ if
              @static and req.path.startswith '/static/' =>
                req.static $ os.path.join @static $
                             os.path.normpath (req.path !! slice 7 None) !! slice 1 None
              otherwise => @handler req
            inner :: Abort          => raise inner
            inner :: CancelledError => raise inner
            inner :: Exception      => req.abort 500  # `inner` is stored as `__context__`.
          outer :: Abort => except
            middle => except
              inner => yield from $ response.responsify $ @onerror req outer
              inner :: Abort          => raise inner
              inner :: CancelledError => raise inner
              inner :: Exception      => req.abort 500
            middle :: Abort => yield from $ response.responsify $ @error req middle

        data = if req.ishead => ()
                  otherwise  => resp.data
        yield from $ http.io_response writer req.version resp.code resp.headers data
        yield from $ resp.communicate reader writer
        close = 'close' in resp.headers.get 'connection' '' or req.version < (1, 1) or
                'close' in req .headers.get 'connection' '' or 'upgrade' in resp.headers
        close => raise CancelledError
    err :: CancelledError       => raise err
    err :: ConnectionError      =>
    err :: ConnectionResetError =>
    err :: Exception => @log.fatal 'error in http handler' exc_info: True, raise CancelledError
    finally => writer.close!

  #: The default error handler. Do not use it. EVER.
  #:
  #: error :: Request Abort -> Response
  #:
  error = req err ~>
    err.code == 500 => @log.error 'unhandled error' exc_info: 1
    err.code, err.headers, '''<!doctype html>
      <html>
        <head>
          <title>{0.code} â€” {0.name}</title>
          <style>p[data-has-url="False"] {{ display: none; }}</style>
        </head>
        <body>
          <h1>{0.code} {0.name}</h1>
          <p>{0.description}</p>
          <p data-has-url="{1}">Though <a href="{2}">a different URL</a> is available.</p>
        </body>
      </html>'''.format err ('location' in err.headers) (err.headers.get 'location')
